PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX up: <http://purl.uniprot.org/core/>

SELECT ?disease_iri ?label ?def
    (GROUP_CONCAT(DISTINCT ?synonym; separator="|") AS ?synonyms)   
    (GROUP_CONCAT(DISTINCT ?abbrev; separator="|") AS ?abbrevs)
    (GROUP_CONCAT(DISTINCT ?xref; separator="|") AS ?xrefs)
    (GROUP_CONCAT(DISTINCT ?keyword; separator="|") AS ?keywords)
    (GROUP_CONCAT(DISTINCT ?keyword_iri; separator="|") AS ?keyword_iris)

WHERE {
    ?disease_iri a up:Disease .
    OPTIONAL{ ?disease_iri skos:altLabel ?synonym  . }
    OPTIONAL{ ?disease_iri skos:prefLabel ?label  . }
    OPTIONAL{ ?disease_iri rdfs:comment ?def  . }
    OPTIONAL{
        ?disease_iri rdfs:seeAlso ?xref  .
        FILTER( !CONTAINS( str(?xref), "keyword") )
    }
    OPTIONAL{ 
        ?disease_iri rdfs:seeAlso ?keyword_iri  .
        FILTER( CONTAINS( str(?keyword_iri), "keyword") )
        ?keyword_iri skos:prefLabel ?keyword .
    }
    OPTIONAL{ ?disease_iri up:mnemonic ?abbrev . }
}
GROUP BY ?disease_iri ?label ?def