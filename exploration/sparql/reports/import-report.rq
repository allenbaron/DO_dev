# Return a count of each import by ORIGINAL namespace
#
# NOTES:
# - QUERY against doid-merged.owl
# - Ignores import roots --> classes are not duplicated in counts
# - ~ 2s execution

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?import (COUNT(DISTINCT(?class)) as ?count)
WHERE {
	?class a owl:Class .
	FILTER(!REGEX(STR(?class), "DOID", "i"))
	FILTER(!isBlank(?class))

	BIND(STRBEFORE(STRAFTER(STR(?class), "obo/"), "_") AS ?import)
} GROUP BY ?import
