# Return a list of all non-obsolete class IDs and labels

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX doid: <http://purl.obolibrary.org/obo/DOID_>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?id ?type ?label
WHERE {
    {
        SELECT ?s ?id
        WHERE {
            ?s a owl:Class ;
                rdfs:subClassOf+ doid:14566 ;
                oboInOwl:id ?id .
            FILTER NOT EXISTS { ?s owl:deprecated ?obsolete . }
        }
    }

    {
        ?s rdfs:label ?label .
        BIND( "preferred" AS ?type )
    }
    UNION
    {
        ?s oboInOwl:hasExactSynonym ?label .
        BIND( "exact_synonym" AS ?type )
    }

} ORDER BY ?id DESC(?type)
