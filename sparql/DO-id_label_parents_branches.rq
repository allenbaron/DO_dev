# Return a list of all non-obsolete class IDs, labels, parents, & branches

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX DOID: <http://purl.obolibrary.org/obo/DOID_>

SELECT DISTINCT ?id ?label
    (GROUP_CONCAT(DISTINCT ?parent_label; SEPARATOR="|") AS ?parents)
    (GROUP_CONCAT(DISTINCT ?branch_label; SEPARATOR="|") AS ?branch)
WHERE {
    ?class a owl:Class ;
        oboInOwl:id ?id ;
        rdfs:label ?label ;
        rdfs:subClassOf ?superclass .

    ?superclass rdfs:label ?parent_label ;
        rdfs:subClassOf* ?branch_iri .

    ?branch_iri rdfs:label ?branch_label ;
        rdfs:subClassOf DOID:4 .

    FILTER NOT EXISTS { ?class owl:deprecated ?obsolete . }
}
GROUP BY ?id ?label