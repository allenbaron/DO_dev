PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?id ?pubmed_src
WHERE {
    ?class a owl:Class ;
        oboInOwl:id ?id ;
        obo:IAO_0000115 ?def .

    FILTER NOT EXISTS { ?class owl:deprecated ?any . }

    ?anon owl:annotatedSource ?class ;
        owl:annotatedProperty obo:IAO_0000115 ;
        owl:annotatedTarget ?def ;
        oboInOwl:hasDbXref ?pubmed_src .

    FILTER(CONTAINS(lcase(str(?pubmed_src)), "pubmed"))
}
